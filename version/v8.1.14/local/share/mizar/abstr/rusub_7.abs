:: Formalization of Orthogonal Decomposition for Hilbert Spaces
::  by Hiroyuki Okazaki
:: 
:: Received December 27, 2022
:: Copyright (c) 2022-2023 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, BHSP_1, PRE_TOPC, REAL_1, COMPLEX1, ARYTM_1, ARYTM_3,
      XXREAL_0, CARD_1, SUBSET_1, RELAT_1, XBOOLE_0, TARSKI, STRUCT_0, FUNCT_1,
      NAT_1, SEQ_2, PROB_2, SQUARE_1, NORMSP_1, SUPINF_2, RSSPACE2, RLVECT_3,
      RLVECT_1, RLSUB_1, VALUED_1, RLSUB_2, RVSUM_1, ORDINAL2, XXREAL_1, SEQ_1,
      RCOMP_1, METRIC_1, XXREAL_2, SEQ_4, MEMBERED, PCOMPS_1, DUALSP04,
      RUSUB_7, RUSUB_5, NORMSP_2, METRIC_6;
 notations TARSKI, XBOOLE_0, SUBSET_1, FUNCT_1, ORDINAL1, RELSET_1, FUNCT_2,
      NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, SQUARE_1, MEMBERED, COMPLEX1,
      XXREAL_1, XXREAL_2, VALUED_1, SEQ_1, SEQ_2, SEQ_4, STRUCT_0, PRE_TOPC,
      RLVECT_1, RLSUB_1, NORMSP_0, NORMSP_1, NFCONT_1, NORMSP_2, TBSP_1,
      METRIC_1, METRIC_6, PCOMPS_1, BHSP_1, BHSP_2, RUSUB_1, RUSUB_2, RUSUB_3,
      RUSUB_5, RSSPACE2, DUALSP04;
 constructors REAL_1, SQUARE_1, COMPLEX1, BHSP_3, RELSET_1, COMSEQ_2, SEQ_4,
      REALSET1, BHSP_2, RUSUB_2, RUSUB_3, RUSUB_5, TBSP_1, METRIC_6, PCOMPS_1,
      SEQ_2, NORMSP_2, NFCONT_1, RCOMP_1, DUALSP04;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, FUNCT_2, RLVECT_1, NUMBERS,
      XREAL_0, STRUCT_0, XCMPLX_0, VALUED_1, SQUARE_1, NAT_1, VALUED_0,
      RUSUB_1, XXREAL_2, SEQ_4, MEMBERED, SEQ_2, TOPS_1, XXREAL_0;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;


begin :: Preliminaries

 reserve X for RealUnitarySpace;
 reserve x, y, y1, y2 for Point of X;

theorem :: RUSUB_7:1
  for X be RealUnitarySpace,
      x,y be Point of X,
      z,t be Point of MetricSpaceNorm RUSp2RNSp X
    st x=z & y = t holds
   ||.x-y.|| = dist(z,t);

theorem :: RUSUB_7:2
  for X be RealUnitarySpace, z be Element of
    MetricSpaceNorm RUSp2RNSp X,
      r be Real holds ex x be Point of X st x = z
    & Ball(z,r) = {y where y is Point of X:||.x-y.|| < r};

theorem :: RUSUB_7:3
  for X be RealUnitarySpace, z be Element of MetricSpaceNorm RUSp2RNSp X,
      r be Real holds ex x be Point of X st x = z &
  cl_Ball(z,r) = {y where y is Point of X: ||.x-y.|| <= r};

theorem :: RUSUB_7:4
  for X be RealUnitarySpace, S be sequence of X,
      St be sequence of MetricSpaceNorm RUSp2RNSp X,
      x be Point of X,
      xt be Point of MetricSpaceNorm RUSp2RNSp X st S = St
  & x = xt holds St is_convergent_in_metrspace_to xt iff
  for r be Real st 0 < r
  ex m be Nat st for n be Nat st m <= n holds ||. S.n - x
  .|| < r;

theorem :: RUSUB_7:5
  for X be RealUnitarySpace, S be sequence of X,
      St be sequence of MetricSpaceNorm RUSp2RNSp X st S = St
    holds St is convergent iff S is convergent;

theorem :: RUSUB_7:6
  for X be RealUnitarySpace, S be sequence of X,
      St be sequence of MetricSpaceNorm RUSp2RNSp X st S = St
 & St is convergent holds lim St = lim S;

begin :: Topological Space generated from Real Unitary Space

theorem :: RUSUB_7:7
  for X be RealUnitarySpace, V be Subset of TopSpaceNorm RUSp2RNSp X
      holds V is open iff for x be Point of X st x in V
  ex r be Real st r>0 &
  {y where y is Point of X: ||.x-y.|| < r} c= V;

theorem :: RUSUB_7:8
  for X be RealUnitarySpace, x be Point of X,
      r be Real holds {y where
  y is Point of X:||.x-y.|| < r} is open Subset of TopSpaceNorm RUSp2RNSp X;

theorem :: RUSUB_7:9
  for X be RealUnitarySpace, x be Point of X, r be Real
  holds {y where y is
  Point of X: ||.x-y.|| <= r} is closed Subset of TopSpaceNorm RUSp2RNSp X;

theorem :: RUSUB_7:10
for M being RealUnitarySpace
for X being Subset of TopSpaceNorm RUSp2RNSp M holds
for x being object holds
( x in Cl X iff ex S being sequence of M st
( ( for n being Nat holds S . n in X ) & S is convergent & lim S = x ) );

theorem :: RUSUB_7:11
for M being RealUnitarySpace
for X being Subset of TopSpaceNorm RUSp2RNSp M holds
( X is closed iff
for S being sequence of M
st ( for n being Nat holds S . n in X ) & S is convergent holds
lim S in X );

theorem :: RUSUB_7:12
  for S be RealUnitarySpace,
      X be Subset of S holds
  X is closed Subset of TopSpaceNorm RUSp2RNSp S
   iff
for s1 being sequence of S st rng s1 c= X & s1 is convergent holds
lim s1 in X;

theorem :: RUSUB_7:13
  for S be RealUnitarySpace,
      x be Point of S,
      y be Point of MetricSpaceNorm RUSp2RNSp S,
      r be Real
st x=y
holds Ball(x,r) = Ball(y,r);

theorem :: RUSUB_7:14
for S be RealUnitarySpace
holds
  TopSpaceNorm RUSp2RNSp S = TopUnitSpace S;

theorem :: RUSUB_7:15
  for S being RealUnitarySpace,
      U be Subset of S,
      V be Subset of TopSpaceNorm RUSp2RNSp S
st U = V
holds
   U is closed iff V is closed;

theorem :: RUSUB_7:16
  for S being RealUnitarySpace,
      U be Subset of S,
      V be Subset of TopSpaceNorm RUSp2RNSp S
st U = V
holds
   U is open iff V is open;

theorem :: RUSUB_7:17
for X being RealUnitarySpace,
    M be Subspace of X,
    x,m0 be Point of X st m0 in M
holds
( for m be Point of X
       st m in M holds ||.x-m0.|| <= ||.x-m.|| )
iff
 for m be Point of X
         st m in M holds ( x-m0 ) .|. m = 0;

theorem :: RUSUB_7:18
for X being RealUnitarySpace,
    M be Subspace of X,
    x,m1,m2 be Point of X
st
m1 in M & m2 in M
&
( for m be Point of X
       st m in M holds ||.x-m1.|| <= ||.x-m.|| )
 &
( for m be Point of X
       st m in M holds ||.x-m2.|| <= ||.x-m.|| )
holds m1=m2;

theorem :: RUSUB_7:19
for X being RealHilbertSpace,
    M be Subspace of X,
    x be Point of X
st
the carrier of M is closed Subset of
TopSpaceNorm RUSp2RNSp X
holds
ex m0 be Point of X st m0 in M
&
 for m be Point of X
       st m in M holds ||.x-m0.|| <= ||.x-m.||;

definition
  let X be RealUnitarySpace,
      M be Subset of X;
  func Ort_CompSet(M) -> non empty Subset of X
  means
:: RUSUB_7:def 1

for x being Point of X
 holds
x in it iff ( for y be Point of X st y in M holds y.|.x = 0  );
end;

theorem :: RUSUB_7:20
for X being RealUnitarySpace,
    M be Subset of X
holds Ort_CompSet(M) is linearly-closed;

theorem :: RUSUB_7:21
for X being RealUnitarySpace,
    M be non empty Subset of X,
    seq be sequence of X
st rng seq c= the carrier of Ort_Comp(M)
   &
  seq is convergent
holds lim seq in the carrier of Ort_Comp(M);

theorem :: RUSUB_7:22
  for S be RealUnitarySpace,
      M be non empty Subset of S,
      L be Subset of S
  st L = the carrier of Ort_Comp(M) holds
    L is closed Subset of TopSpaceNorm RUSp2RNSp S;

theorem :: RUSUB_7:23
  for X being RealUnitarySpace,
      M be non empty Subset of X holds
    M is Subset of Ort_Comp Ort_Comp M;

theorem :: RUSUB_7:24
for X being RealUnitarySpace,
    S,T be non empty Subset of X
 st S c= T
holds
Ort_Comp (T) is Subspace of Ort_Comp(S);

theorem :: RUSUB_7:25
for X being RealHilbertSpace,
    M be Subspace of X
st
X is strict &
the carrier of M is closed Subset of TopSpaceNorm RUSp2RNSp X
holds X is_the_direct_sum_of M,Ort_Comp(M);

theorem :: RUSUB_7:26
for X being RealHilbertSpace,
    M be strict Subspace of X
st
X is strict
&
the carrier of M is closed Subset of TopSpaceNorm RUSp2RNSp X
holds M = Ort_Comp Ort_Comp M;

theorem :: RUSUB_7:27
for X being RealUnitarySpace,
    M be Subspace of X,
    K be Subset of X,
    L be Subset of TopSpaceNorm RUSp2RNSp X
st the carrier of M = L & K = Cl(L) holds
      K is linearly-closed;

theorem :: RUSUB_7:28
for X being RealHilbertSpace,
    M be non empty Subset of X
st X is strict
holds
  the carrier of Ort_Comp Ort_Comp M
                 is closed Subset of TopSpaceNorm RUSp2RNSp X
    &
   ( ex L be Subset of TopSpaceNorm RUSp2RNSp X
       st L = the carrier of Lin M
         &
         the carrier of Ort_Comp(Ort_Comp(M)) = Cl(L) )
    &
    Lin M is Subspace of Ort_Comp(Ort_Comp(M));

theorem :: RUSUB_7:29
for X being RealHilbertSpace,
    K be strict Subspace of X,
    M be non empty Subset of X
st X is strict
    &
    the carrier of K is closed Subset of TopSpaceNorm RUSp2RNSp X
    &
    Lin M is Subspace of K holds
  Ort_Comp Ort_Comp M is Subspace of K;
