:: Prime Representing Polynomial with 10 Unknowns -- Introduction. Part II
::  by Karol P\kak
:: 
:: Received December 27, 2022
:: Copyright (c) 2022-2023 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, SUBSET_1, RELAT_1, ORDINAL4, FINSEQ_1, FUNCT_1,
      XXREAL_0, NAT_1, INT_1, ARYTM_3, ZFMISC_1, CARD_1, ARYTM_1, COMPLEX1,
      SQUARE_1, NEWTON, INT_2, REAL_1, CARD_3, REALSET1, PELLS_EQ, PYTHTRIP,
      MCART_1, HILB10_1, XBOOLE_0, TARSKI, HILB10_8;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, FUNCT_1, XCMPLX_0,
      FINSEQ_1, XXREAL_0, XREAL_0, INT_1, SQUARE_1, NEWTON, RVSUM_1, NAT_D,
      PELLS_EQ, PYTHTRIP, ABSVALUE, HILB10_1, ZFMISC_1, XTUPLE_0, INT_2, NAT_1,
      PEPIN;
 constructors NAT_D, RELSET_1, NEWTON, WSIERP_1, PELLS_EQ, DOMAIN_1, PEPIN,
      ABIAN, HILB10_1;
 registrations RELAT_1, ORDINAL1, XREAL_0, NAT_1, VALUED_0, INT_1, XXREAL_0,
      XCMPLX_0, NEWTON, NEWTON02, NAT_6, FINSEQ_1, NEWTON04, NEWTON03,
      PYTHTRIP, ABSVALUE, HILB10_1, XTUPLE_0, FOMODEL0, XBOOLE_0, SQUARE_1,
      NUMBERS, CARD_1;
 requirements NUMERALS, SUBSET, ARITHM, REAL;


begin :: Theta Notation

reserve A for non trivial Nat,
        B,C,n,m,k for Nat,
        e for Nat;

definition
   let t be Real;
   attr t is theta means
:: HILB10_8:def 1
     -1 <= t <= 1;
end;

registration
  cluster 0 -> theta;
  cluster theta for Real;
end;

definition
  mode _Theta is theta Real;
end;

registration
  let t be _Theta;
  cluster -t -> theta;
  let u be _Theta;
  cluster t*u -> theta;
end;

theorem :: HILB10_8:1
  for T be _Theta holds |.T.| <=1;

theorem :: HILB10_8:2
  for T be _Theta, lambda, epsilon1,epsilon2 be Real st
     lambda = T * epsilon1 & |.epsilon1.| <= |.epsilon2.|
   ex T1 be _Theta st lambda = T1 * epsilon2;

theorem :: HILB10_8:3
  for T1,T2 be _Theta, lambda, epsilon1,epsilon2 be Real st
     lambda = (1+ T1 * epsilon1) * (1+ T2 * epsilon2) & 0 <= epsilon1 <=1 &
        0<= epsilon2
  holds ex T be _Theta st lambda = 1+ T * (epsilon1 + 2*epsilon2);

theorem :: HILB10_8:4
  for T1,T2 be _Theta,epsilon1,epsilon2 be Real st
    T1*epsilon1 <= epsilon2 & epsilon2 <= T2 * epsilon1
  holds ex T be _Theta st epsilon2 = T * epsilon1;

theorem :: HILB10_8:5
  for T be _Theta, lambda, epsilon1,epsilon2 be Real st
     lambda = T * epsilon1 & epsilon1 <= epsilon2 & 0 <= epsilon1
   ex T1 be _Theta st lambda = T1 * epsilon2;

theorem :: HILB10_8:6
  for T1,T2 be _Theta,epsilon1,epsilon2 be Real st
    0 <= epsilon1 & 0 <= epsilon2
  holds ex T be _Theta st T1*epsilon1 + T2*epsilon2 = T* (epsilon1+epsilon2);

theorem :: HILB10_8:7
  for T1 be _Theta, epsilon be Real st 0<= epsilon <= 1/2
    ex T2 be _Theta st
          1/ (1 + T1*epsilon) = 1 + T2*2* epsilon;

theorem :: HILB10_8:8
  m^2 <= n implies ex T be _Theta st n choose m = n|^m / (m!) * (1+T*(m^2/n));

theorem :: HILB10_8:9
  for T be _Theta, alpha, epsilon be Real st
      alpha = (1+T*epsilon)|^n & 0<= epsilon <= 1 / (2*n) holds
  ex T1 be _Theta st alpha = 1+T1*2*n*epsilon;

begin :: More on Solutions to Pell's Equation

reserve a for non trivial Nat;

theorem :: HILB10_8:10
  n <= a implies ex T be _Theta st
    Py(a,n+1) = (2*a) |^n *(1+T*(n/a));

theorem :: HILB10_8:11
  for a be non trivial Nat,y,n be Nat st
    y>0 &
    n>0 &
    (a^2-1)*y^2 +1 is square &
    y,n are_congruent_mod a-1 &
    y <= Py(a,a-1) &
    n<= a-1
  holds y = Py(a,n);

theorem :: HILB10_8:12
  for a be non trivial Nat,s,n be Nat holds
    s^2*(s|^n)^2-(s^2-1)*Py(a,n+1)*(s|^n)-1,0 are_congruent_mod 2*a*s-s^2-1;

theorem :: HILB10_8:13
  for a be non trivial Nat,s,n,r be Nat st
    s > 0 & r > 0 &
    s^2*r^2 - (s^2-1)*Py(a,n+1)*r -1,0 are_congruent_mod 2*a*s-s^2-1 &
    s*((s|^n)^2*(s|^n))< a &
    s*(r^2*r) < a
  holds r = s|^n;

theorem :: HILB10_8:14
  for a,b,c,d be Nat st a <= b <= c & 2*c <= d & c > 0
    for f be FinSequence of REAL st
      len f = b-a+1 &
      for i be Nat st i+1 in dom f holds f.(i+1) =
         (c choose (a+i)) * (d |^(c-'(a+i)))
    holds
      0 < Sum f  < 2*(c|^a) * (d|^(c-'a));

theorem :: HILB10_8:15
  for f,k,m,r,s,t,u be Nat,W,M,U,S,T,Q be Integer st
    f>0 & k >0 & m>0 & u>0 &
    (M^2-1)*S^2 +1 is square &
    ((M*U)^2 -1)*T^2 + 1 is square &
    W^2*u^2 - (W^2-1)*S*u-1,0 are_congruent_mod Q &
    (4*f^2 -1)*(r-m*S*T*U)^2 + 4*u^2*S^2*T^2 < 8*f*u*S*T*(r-m*S*T*U) &
    W = 100*f*k*(k+1) &
    U = 100 * (u|^3)*(W|^3)+1 &
    M = 100 * m * U *W+1 &
    S = (M-1)*s+k+1 &
    T = (M*U-1)*t +W-k+1 &
    Q = 2*M*W-W^2 -1
  holds
    M*(U+1) is non trivial Nat & W is Nat &
    for mu be non trivial Nat,w be Nat st mu = M*(U+1) & w = W &
        r + W + 1 = Py(mu,w+1) holds f = k!;

theorem :: HILB10_8:16
  for f,k be Nat st f = k! & k > 0 holds
    ex m,r,s,t,u be Nat,W,U,S,T,Q be Nat, M be non trivial Nat st
      m>0 & u>0 &
      r + W + 1 = Py(M*(U+1),W+1) &
      (M^2-1)*S^2 +1 is square &
      ((M*U)^2 -1)*T^2 + 1 is square &
      W^2*u^2 - (W^2-1)*S*u-1,0 are_congruent_mod Q &
      (4*f^2 -1)*(r-m*S*T*U)^2 + 4*u^2*S^2*T^2 < 8*f*u*S*T*(r-m*S*T*U) &
      W = 100*f*k*(k+1) &
      U = 100 * (u|^3)*(W|^3)+1 &
      M = 100 * m * U *W+1 &
      S = (M-1)*s+k+1 &
      T = (M*U-1)*t +W-k+1 &
      Q = 2*M*W-W^2 -1;

theorem :: HILB10_8:17
  for A be non trivial Nat for C,B be Nat, e st 0 < B holds
    C = Py(A,B) implies
  ex i,j be Nat, D,E,F,G,H,I be Nat st
    D*F*I is square &
    F divides (H - C) &
    B <= C &

  D = (A^2-1)*C^2+1 & E = 2*(i+1)*D*(e+1)*C^2 & F= (A^2 -1) *E^2+1 &
  G = A+F*(F-A) & H = B+2*j*C & I = (G^2-1)*H^2+1;

theorem :: HILB10_8:18
  for A be non trivial Nat for C,B be Nat, e be Nat st 0 < B
    for i,j be Nat, D,E,F,G,H,I be Integer st
      D*F*I is square & F divides (H - C) & B <= C &
  D= (A^2-1)*C^2+1 & E= 2*(i+1)*D*(e+1)*C^2 & F= (A^2 -1) *E^2+1 &
  G = A+F*(F-A) & H = B+2*j*C & I = (G^2-1)*H^2+1
    holds C = Py(A,B);

::$N Diophantine Representation of Solutions to Pell's Equation
::with 2 Explicite Unknowns $i$, $j$
theorem :: HILB10_8:19
:: Theorem 4
:: Y. Matiyasevich, J Robinson:
:: Reduction of an Arbitrary Diophantine Equation to One in 13 Unknowns
:: Acta Arithmetica, 27, 1975.
  for A be non trivial Nat,C,B be Nat,e st 0 < B holds
    C = Py(A,B) iff
      ex i,j be Nat, D,E,F,G,H,I be Integer st
        D*F*I is square & F divides (H - C) & B <= C &
   D= (A^2-1)*C^2+1 & E= 2*(i+1)*D*(e+1)*C^2 & F= (A^2 -1) *E^2+1 &
   G = A+F*(F-A) & H = B+2*j*C & I = (G^2-1)*H^2+1;

theorem :: HILB10_8:20
 for A be non trivial Nat, C be Nat, B,L be positive Nat holds
   C = Py(A,B) iff
     ex i,j be positive Nat, D,E,F,G,H,I be Integer st
       D*F*I is square & F divides (H - C) & B <= C &

  D = (A^2-1)*C^2+1 & E = 2*i*C^2*L*D & F = (A^2 -1) *E^2+1 &
  G = A+F*(F-A) & H = B+2*(j-1)*C & I = (G^2-1)*H^2+1;

begin :: Prime Diophantine Representation

theorem :: HILB10_8:21
  for k be Nat, L be positive Nat st k>0 holds
    k+1 is prime iff
    ex f,i,j,m,u be positive Nat,r,s,t be Nat,
       A,B,C,D,E,F,G,H,I,W,U,M,S,T,Q be Integer st
    D*F*I is square & F divides (H - C) &
    (M^2-1)*S^2 +1 is square &
    ((M*U)^2 -1)*T^2 + 1 is square &
    W^2*u^2 - (W^2-1)*S*u-1,0 are_congruent_mod Q &
    (4*f^2 -1)*(r-m*S*T*U)^2 + 4*u^2*S^2*T^2 < 8*f*u*S*T*(r-m*S*T*U) &
    k+1 divides f+1 &

  A = M*(U+1) & B = W+1 & C = r + W + 1 & D= (A^2-1)*C^2+1 & E= 2*i*C^2*L*D &
  F= (A^2-1)*E^2+1 & G = A+F*(F-A) & H = B+2*(j-1)*C & I = (G^2-1)*H^2+1 &
  W = 100*f*k*(k+1) & U = 100*(u|^3)*(W|^3)+1 & M = 100*m*U*W+1 &
  S = (M-1)*s+k+1 & T = (M*U-1)*t+W-k+1 & Q = 2*M*W-W^2-1;

theorem :: HILB10_8:22
  for a,b,A,B be Integer st a,b are_coprime holds
    a divides A & b divides B iff a*b divides a*B + b*A;

::$N Diophantine Representation of Prime Numbers with 8 Explicite Unknowns

theorem :: HILB10_8:23
  for k be Nat st k>0 holds
    k+1 is prime iff
    ex f,i,j,m,u be positive Nat,r,s,t be Nat,
        A,B,C,D,E,F,G,H,I,L,W,U,M,S,T,Q be Integer st
      D*F*I is square & (M^2-1)*S^2 +1 is square &
      ((M*U)^2 -1)*T^2 + 1 is square &
      (4*f^2 -1)*(r-m*S*T*U)^2 + 4*u^2*S^2*T^2 < 8*f*u*S*T*(r-m*S*T*U) &
      F*L divides (H-C)*L + F*(f+1)*Q + F*(k+1) *((W^2-1)*S*u-W^2*u^2 +1) &

A = M*(U+1) & B = W+1 & C = r + W + 1 & D = (A^2-1)*C^2+1 & E = 2*i*C^2*L*D &
F= (A^2 -1) *E^2+1 & G = A+F*(F-A) & H = B+2*(j-1)*C & I = (G^2-1)*H^2+1 &
L = (k+1)*Q & W = 100*f*k*(k+1) & U = 100 * (u|^3)*(W|^3)+1 &
M = 100 * m * U *W+1 & S = (M-1)*s+k+1 & T = (M*U-1)*t +W-k+1 & Q = 2*M*W-W^2-1
;
