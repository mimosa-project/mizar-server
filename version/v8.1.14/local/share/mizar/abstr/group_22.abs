:: Characteristic Subgroups
::  by Alexander M. Nelson
:: 
:: Received July 23, 2022
:: Copyright (c) 2022-2023 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies MOD_4, GROUP_22, CARD_3, QC_LANG1, RLSUB_1, RELAT_1, TARSKI,
      FUNCT_1, ZFMISC_1, XXREAL_1, FINSEQ_1, FINSET_1, NUMBERS, WELLORD1,
      SUBSET_1, XBOOLE_0, PARTFUN1, FUNCT_2, CARD_1, ARYTM_3, CQC_SIM1,
      ORDINAL1, EQREL_1, STRUCT_0, GROUP_1, GROUP_2, GROUP_3, GROUP_4, GROUP_5,
      GROUP_6, BINOP_1, BINOP_2, ALGSTR_0, REALSET1, AUTGROUP, GR_CY_1,
      NATTRA_1, NAT_1, INT_2, SETFAM_1, NEWTON, PRE_TOPC, GROUP_10, WEDDWITT;
 notations TARSKI, XBOOLE_0, SUBSET_1, XCMPLX_0, ORDINAL1, FUNCT_1, RELSET_1,
      FUNCT_2, PARTFUN1, INT_1, INT_2, SETFAM_1, NUMBERS, BINOP_2, FINSET_1,
      STRUCT_0, ALGSTR_0, FINSEQ_1, GROUP_1, GROUP_2, GROUP_3, GROUP_4,
      GROUP_5, REALSET1, NAT_D, GRSOLV_1, AUTGROUP, GROUP_10, GR_CY_1, NEWTON,
      GROUP_6;
 constructors RELSET_1, FINSOP_1, NAT_D, BINOP_2, REALSET1, MONOID_0, GROUP_4,
      GROUP_5, GRSOLV_1, AUTGROUP, GROUP_10, NEWTON;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, RELSET_1, FUNCT_2, ORDINAL1,
      FINSET_1, NUMBERS, NAT_1, INT_1, INT_2, XREAL_0, CARD_1, STRUCT_0,
      GROUP_1, GROUP_2, GROUP_3, GROUP_6, GR_CY_1, FOMODEL0;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM;


begin :: Preparatory Work

reserve X for set;

theorem :: GROUP_22:1
  for a,b,c being Nat st c <> 0 & c*a divides c*b holds a divides b;

theorem :: GROUP_22:2
  for a,b,c being Nat
  st b<>0 & b divides c & a*b,c are_coprime
  holds b=1;

theorem :: GROUP_22:3
  for G1,G2 being Group
  for H being Subgroup of G1
  for f being Homomorphism of G1,G2
  for h being Element of G1
  st h in H
  holds (f|H).h = f.h;

theorem :: GROUP_22:4
  for X,Y being non empty set
  for f being Function of X,Y
  st f is bijective
  for y being Element of Y
  holds f.((f").y) = y;

theorem :: GROUP_22:5
  for X,Y being non empty set
  for A being non empty Subset of X
  for x being Element of X
  st not x in A
  for f being Function of X,Y
  st f is one-to-one
  holds not f.x in (f .: A);

begin :: Nontrivial Groups and Subgroups

registration
  cluster strict non trivial for Group;
end;

registration
  let G be Group;
  cluster trivial for Subgroup of G;
  let H be Subgroup of G;
  cluster trivial for Subgroup of H;
end;

registration
  let G be non trivial Group;
  cluster non trivial for Subgroup of G;

  cluster strict non trivial for Subgroup of G;
end;

theorem :: GROUP_22:6
  for G being Group
  holds G is trivial iff the multMagma of G = (1).G;

registration
  cluster non trivial for finite Group;
end;

theorem :: GROUP_22:7
  for G being Group
  for H being Subgroup of G
  st H is trivial
  holds the multMagma of H = (1).G;

theorem :: GROUP_22:8
  for G being Group
  for H being trivial Subgroup of G
  for K being trivial Subgroup of G
  holds the multMagma of H = the multMagma of K;

theorem :: GROUP_22:9
  for G being Group
  for K being trivial Subgroup of G
  for H being Subgroup of G
  st H is Subgroup of K
  holds H is trivial Subgroup of G;

begin :: Proper Subgroups

definition
  let G be Group;
  let IT be Subgroup of G;
  attr IT is proper means
:: GROUP_22:def 1

  the multMagma of IT <> the multMagma of G;
end;

reserve G for Group;
reserve H for Subgroup of G;

theorem :: GROUP_22:10
  H is proper iff the carrier of H <> the carrier of G;

reserve h,x,y for object;

theorem :: GROUP_22:11
  H is proper iff (the carrier of G) \ (the carrier of H) is non empty set;

registration
  let G be non trivial Group;
  cluster strict proper for Subgroup of G;
end;

registration
  let G be non trivial Group;
  cluster maximal -> proper for Subgroup of G;
end;

theorem :: GROUP_22:12
  for G being non trivial Group
  for H being proper Subgroup of G
  for K being Subgroup of G
  st H is Subgroup of K & the multMagma of H <> the multMagma of K
  holds K is non trivial Subgroup of G;

begin :: Automorphisms

definition
  let G;
  mode Endomorphism of G is Homomorphism of G,G;
end;

reserve f for Endomorphism of G;

registration
  let G;
  cluster bijective for Endomorphism of G;
end;

definition
  let G;
  mode Automorphism of G is bijective Endomorphism of G;
end;

reserve phi for Automorphism of G;

theorem :: GROUP_22:13
  Image (f|(1).G) = (1).G;

:: In particular, the trivial proper subgroup (1).G of G is invariant
:: under inner automorphisms, and thus is a characteristic subgroup.
theorem :: GROUP_22:14
  Image(phi|(1).G) is Subgroup of (1).G;

theorem :: GROUP_22:15
  for G1,G2 being Group
  for f being Homomorphism of G1,G2
  for H being Subgroup of G1
  holds Ker(f|H) is Subgroup of Ker(f);

theorem :: GROUP_22:16
  (for f being Automorphism of G holds Image(f|H) is Subgroup of H) implies
  ex psi being Automorphism of G
  st psi = phi" & Image(phi|Image(psi|H)) is Subgroup of Image(phi|H);

theorem :: GROUP_22:17
  ex psi being Automorphism of G
  st psi = phi" & Image(phi|Image(psi|H)) = the multMagma of H;

theorem :: GROUP_22:18
  for H being strict Subgroup of G
  for K being Subgroup of G
  st Image(phi|H) is Subgroup of K
  holds ex psi being Automorphism of G
  st psi = phi" & H is Subgroup of Image(psi|K);

theorem :: GROUP_22:19
  H,phi .: H are_isomorphic;

theorem :: GROUP_22:20
  for G being finite Group
  for H1,H2 being strict Subgroup of G
  st H1,H2 are_isomorphic
  holds index H1 = index H2;

theorem :: GROUP_22:21
  G is finite implies
  for p being prime Nat
  for P being strict Subgroup of G
  st P is_Sylow_p-subgroup_of_prime p
  holds Image(phi|P) is_Sylow_p-subgroup_of_prime p;

theorem :: GROUP_22:22
  for f being Automorphism of G
  st Image(f|H) = the multMagma of H
  holds f|H is Automorphism of H;

theorem :: GROUP_22:23
  for G being non trivial Group
  for H being Subgroup of G
  for phi being Automorphism of G
  st H is proper Subgroup of G
  holds Image(phi|H) is proper Subgroup of G;

theorem :: GROUP_22:24
  for G being non trivial Group
  for H being strict Subgroup of G
  for phi being Automorphism of G
  st H is maximal
  holds Image(phi|H) is maximal;

begin :: Inner Automorphisms

definition
  let G;
  let a be Element of G;
  let f be Function;
  pred a is_inner_wrt f means
:: GROUP_22:def 2

  for x being Element of G holds f.x = x |^ a;
end;

definition
  let G;
  let IT be Automorphism of G;
  attr IT is inner means
:: GROUP_22:def 3

  ex a being Element of G st a is_inner_wrt IT;
end;

notation
  let G be Group, f be Automorphism of G;
  antonym f is outer for f is inner;
end;

registration
  let G;
  cluster inner for Automorphism of G;
end;

theorem :: GROUP_22:25
  for G being strict Group, f being object
  holds f in Aut G iff f is Automorphism of G;

theorem :: GROUP_22:26
  for G being strict Group
  for f being object
  holds f in InnAut G iff f is inner Automorphism of G;

theorem :: GROUP_22:27
  for a being Element of G
  for f being inner Automorphism of G st a is_inner_wrt f
  holds Image(f|H) = H |^ a;

theorem :: GROUP_22:28
  for a being Element of G
  for f being Endomorphism of G st a is_inner_wrt f
  holds Ker f = (1).G;

theorem :: GROUP_22:29
  for a being Element of G
  for f being Endomorphism of G st a is_inner_wrt f
  holds f is Automorphism of G;

theorem :: GROUP_22:30
  for a being Element of G
  for f being Endomorphism of G st a is_inner_wrt f
  holds f is inner Automorphism of G;

theorem :: GROUP_22:31
  for a being Element of G
   ex f being inner Automorphism of G st a is_inner_wrt f;

theorem :: GROUP_22:32
  for H being strict Subgroup of G
  holds H is normal iff for f being inner Automorphism of G holds Image(f|H)=H;

begin :: Characteristic Subgroups

:: Dummit and Foote, Abstract Algebra, ch.4 section 4

definition
  let G;
  let IT be Subgroup of G;
  attr IT is characteristic means
:: GROUP_22:def 4

  for f being Automorphism of G holds Image (f|IT) = the multMagma of IT;
end;

registration
  let G;
  cluster (1).G -> characteristic;
end;

registration
  let G;
  cluster characteristic for Subgroup of G;
end;

reserve K for characteristic Subgroup of G;

registration
  let G be Group;
  cluster strict characteristic for Subgroup of G;
end;

theorem :: GROUP_22:33
  K is normal Subgroup of G;

registration
  let G be Group;
  cluster characteristic -> normal for Subgroup of G;
end;

theorem :: GROUP_22:34
  for G1,G2 being Group
  for H1 being Subgroup of G1
  for K being Subgroup of H1
  for H2 being Subgroup of G2
  for f being Homomorphism of G1,G2
  for g being Homomorphism of H1,H2
  st (for k being Element of G1 st k in K holds f.k=g.k)
  holds Image(f|K) = Image(g|K);

theorem :: GROUP_22:35
  for H being strict Subgroup of G
  st (for K being strict Subgroup of G
      st card K = card H
      holds H = K)
  holds H is characteristic;

theorem :: GROUP_22:36
  for N being strict normal Subgroup of G
  for K being characteristic Subgroup of N
  holds K is normal Subgroup of G;

theorem :: GROUP_22:37
  for N being characteristic Subgroup of G
  for K being characteristic Subgroup of N
  holds K is characteristic Subgroup of G;

theorem :: GROUP_22:38
  for G being Group
  for H being strict Subgroup of G
  holds H is characteristic Subgroup of G iff
  for phi being Automorphism of G holds Image(phi|H) is Subgroup of H;

theorem :: GROUP_22:39
  center G is characteristic Subgroup of G;

scheme :: GROUP_22:sch 1 :: sch 1
  CharMeet{G() -> Group, P[set]} :
  for phi being Automorphism of G()
  holds phi .: meet{A where A is Subset of G() : ex K being strict Subgroup
  of G() st A = the carrier of K & P[K]} = meet{A where A is Subset of G() :
  ex K being strict Subgroup of G() st A = the carrier of K & P[K]}
provided
 for phi being Automorphism of G()
    for H being strict Subgroup of G()
    st P[H]
    holds P[Image(phi|H)] and
 ex H being strict Subgroup of G() st P[H];

scheme :: GROUP_22:sch 2 :: sch 2
  MeetIsChar{G() -> Group, P[set]} :
  ex K being strict Subgroup of G() st
  the carrier of K = meet {A where A is Subset of G() :
  ex H being strict Subgroup of G() st A = the carrier of H & P[H]} &
  K is characteristic
provided
 for phi being Automorphism of G()
    for H being strict Subgroup of G()
    st P[H]
    holds P[Image(phi|H)] and
 ex H being strict Subgroup of G() st P[H];

theorem :: GROUP_22:40
  for G being non trivial Group
  holds (ex H being strict Subgroup of G st H is maximal) implies
  Phi(G) is characteristic Subgroup of G;

theorem :: GROUP_22:41
  for phi being Automorphism of G
  holds phi .: commutators G = commutators G;

theorem :: GROUP_22:42
  for G being Group
  for phi being Automorphism of G
  for H being Subgroup of G
  st (for h being Element of H holds phi.h in H)
  holds Image(phi|H) is Subgroup of H;

theorem :: GROUP_22:43
  for G being Group
  for A being non empty Subset of G
  st (for phi being Automorphism of G holds phi .: A = A)
  holds gr A is characteristic;

theorem :: GROUP_22:44
  G` is characteristic;

theorem :: GROUP_22:45
  for G1,G2 being Group
  for H being Subgroup of G1
  for a being Element of G1
  for f being Homomorphism of G1,G2
  holds f.:(a * H) = (f.a) * (f .: H);

theorem :: GROUP_22:46
  for G1,G2 being Group
  for H being Subgroup of G1
  for a being Element of G1
  for f being Homomorphism of G1,G2
  holds f.:(H * a) = (f .: H) * (f.a);

theorem :: GROUP_22:47
  for G being Group
  for N being strict normal Subgroup of G
  for phi being Automorphism of G
  holds Image(phi|N) is normal Subgroup of G;

theorem :: GROUP_22:48
  for G being Group
  for H being strict Subgroup of G
  holds H is characteristic iff
        (for phi being Automorphism of G
         for x being Element of G
         st x in H
         holds phi.x in H);

theorem :: GROUP_22:49
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds H /\ K is characteristic Subgroup of G;

theorem :: GROUP_22:50
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds H "\/" K is characteristic Subgroup of G;

theorem :: GROUP_22:51
  for G being Group
  for H,K being strict characteristic Subgroup of G
  for phi being Automorphism of G
  holds phi .: commutators(H,K) = commutators(H,K);

theorem :: GROUP_22:52
  for G being Group
  for H,K being strict characteristic Subgroup of G
  holds [.H,K.] is characteristic Subgroup of G;

begin :: Results concerning meets

scheme :: GROUP_22:sch 3 :: sch3
  MeetIsMinimal{G() -> Group, P[set]} :
  ex H being strict Subgroup of G() st
  the carrier of H = meet {A where A is Subset of G() :
                           ex K being strict Subgroup of G()
                           st A = the carrier of K & P[K]} &
  (for K being strict Subgroup of G() st P[K] holds H is Subgroup of K)
provided
 ex H being strict Subgroup of G() st P[H];

theorem :: GROUP_22:53
  for G being Group
  for H1,H2 being Subgroup of G
  st H1 is Subgroup of H2
  for a being Element of G
  holds H1 |^ a is Subgroup of H2 |^ a;

scheme :: GROUP_22:sch 4 :: sch4
  MeetOfNormsIsNormal{G() -> Group, P[set]} :
  for H being strict Subgroup of G()
  st the carrier of H = meet {A where A is Subset of G() :
                              ex N being strict Subgroup of G()
                              st A = the carrier of N & N is normal & P[N]}
  holds H is strict normal Subgroup of G()
provided
 ex H being strict normal Subgroup of G() st P[H];

theorem :: GROUP_22:54
  for G being Group
  for X being finite set
  st X <> {} & (for A being Element of X
                ex N being strict normal Subgroup of G
                st A = the carrier of N)
  ex N being strict normal Subgroup of G
  st the carrier of N = meet X;

begin :: Centralizer of Characteristic Subgroups is Characteristic

definition
  let G be Group;
  let A be Subset of G;
  func Centralizer A -> strict Subgroup of G means
:: GROUP_22:def 5

  the carrier of it = { b where b is Element of G :
                        for a being Element of G st a in A holds a*b = b*a };
end;

theorem :: GROUP_22:55
  for G being Group
  for A being Subset of G
  for g being Element of G
  holds (for a being Element of G st a in A holds g*a = a*g) iff
        g is Element of Centralizer A;

theorem :: GROUP_22:56
  for G being Group
  for A,B being Subset of G
  st A c= B
  holds Centralizer B is Subgroup of Centralizer A;

definition
  let G be Group;
  let H be Subgroup of G;
  func Centralizer H -> strict Subgroup of G means
:: GROUP_22:def 6

  it = Centralizer carr H;
end;

theorem :: GROUP_22:57
  for G being Group
  for H being Subgroup of G
  holds the carrier of Centralizer H = {b where b is Element of G : for a
  being Element of G st a in H holds b*a=a*b};

theorem :: GROUP_22:58
  for G being Group
  for H being Subgroup of G
  for g being Element of G
  holds (for a being Element of G st a in H holds g*a = a*g) iff
        g is Element of Centralizer H;

theorem :: GROUP_22:59
  for G being Group
  for A being Subset of G
  holds A is Subset of Centralizer (Centralizer A);

theorem :: GROUP_22:60
  for G being Group
  for K being strict characteristic Subgroup of G
  holds (Centralizer K) is characteristic Subgroup of G;

definition
  let G be Group;
  let a be Element of G;
  redefine func {a} -> Subset of G;
end;

definition
  let G be Group;
  let a be Element of G;
  func Normalizer a -> strict Subgroup of G equals
:: GROUP_22:def 7
  Normalizer{a};
end;

theorem :: GROUP_22:61
  for G being Group
  for a,x being Element of G
  holds x in Normalizer a iff ex h being Element of G st x = h & a |^ h = a;

theorem :: GROUP_22:62
  for G being Group
  for A being non empty Subset of G
  holds the carrier of Centralizer A = meet {B where B is Subset of G :
  ex H being strict Subgroup of G st B = the carrier of H &
  (ex a being Element of G st a in A & H = Normalizer a)};

theorem :: GROUP_22:63
  for G being finite Group
  for H1,H2 being strict Subgroup of G
  st card(H1 /\ H2) = card H1 & card(H1 /\ H2) = card H2
  holds H1 = H2;

theorem :: GROUP_22:64
  for G1,G2 being finite Group
  for N1 being normal Subgroup of G1
  for N2 being normal Subgroup of G2
  st G1./.N1, G2./.N2 are_isomorphic
  holds card(N2)*card(G1)=card(N1)*card(G2);

theorem :: GROUP_22:65
  for G being finite Group
  for K,N being strict normal Subgroup of G
  for m,d being Nat
  st m = card N & m = card K & d = card(K /\ N)
  holds d*card(N "\/" K) = m*m;

theorem :: GROUP_22:66
  for G being finite Group
  for N being strict normal Subgroup of G
  st card N, index N are_coprime
  holds N is characteristic Subgroup of G;

theorem :: GROUP_22:67
  for G1,G2,G3 being Group
  for f1 being Homomorphism of G1,G2
  for f2 being Homomorphism of G2,G3
  for A being Subgroup of G1
  holds the multMagma of f2 .: (f1 .: A) = the multMagma of ((f2 * f1) .: A);

theorem :: GROUP_22:68
  for G being Group
  for N being strict normal Subgroup of G
  for phi being Automorphism of G
  st Image(phi|N) = N
  ex sigma being Automorphism of G./.N
  st (for x being Element of G holds sigma.(x*N) = (phi.x)*N);

theorem :: GROUP_22:69
  for G being finite Group
  for H being strict characteristic Subgroup of G
  for K being strict Subgroup of G
  st H is Subgroup of K
  holds H is normal Subgroup of K;

:: Gorenstein, Finite Groups, Theorem 2.1.2 (iv)
theorem :: GROUP_22:70
  for G being finite Group
  for H being strict characteristic Subgroup of G
  for K being strict Subgroup of G
  st H is Subgroup of K &
  K./.(K,H)`*` is characteristic Subgroup of G./.H
  holds K is characteristic Subgroup of G;

theorem :: GROUP_22:71
  for G being Group
  for H being Subgroup of G
  holds H is Subgroup of Centralizer H iff H is commutative Group;

theorem :: GROUP_22:72
  for G being Group
  holds Centralizer (Omega).G = center G;

theorem :: GROUP_22:73
  for G being Group
  for N being normal Subgroup of G
  holds Centralizer N is normal Subgroup of G;

theorem :: GROUP_22:74
  for G being Group
  for H being Subgroup of G
  for h,n being Element of G
  st h in H & n in Normalizer H
  holds h |^ n in H;

theorem :: GROUP_22:75
  for G being Group
  for H being Subgroup of G
  holds H is Subgroup of Normalizer H;

theorem :: GROUP_22:76
  for G being Group
  for H being Subgroup of G
  holds Centralizer H is strict normal Subgroup of Normalizer H;
