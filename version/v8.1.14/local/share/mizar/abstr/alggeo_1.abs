:: {I}ntroduction to {A}lgebraic {G}eometry
::  by Yasushige Watase
:: 
:: Received June 30, 2023
:: Copyright (c) 2023 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, SUBSET_1, RELAT_1, ORDINAL4, FINSEQ_1, XBOOLE_0,
      FUNCT_1, XXREAL_0, TARSKI, NAT_1, ARYTM_3, ZFMISC_1, CARD_1, YELLOW_8,
      ORDINAL1, ARYTM_1, PRE_POLY, GROUP_1, ALGSTR_0, RLVECT_1, VECTSP_1,
      POLYNOM1, POLYNOM2, FINSEQ_2, CARD_3, SQUARE_1, STRUCT_0, PARTFUN1,
      FINSET_1, SUPINF_2, RATFUNC1, FUNCT_2, HILBASIS, VECTSP_2, CARD_FIL,
      RSSPACE, IDEAL_1, MESFUNC1, INT_2, SETFAM_1, XCMPLX_0, POLYNOM5,
      LATTICES, BSPACE, NEWTON, RING_1, RING_2, MATROID0, SGRAPH1, FUNCT_4,
      UPROOTS, BINOP_1, ALGGEO_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, RELAT_1, FUNCT_1, ORDINAL1,
      RELSET_1, PARTFUN1, FUNCT_2, BINOP_1, DOMAIN_1, FINSET_1, CARD_1,
      NUMBERS, XXREAL_0, NAT_1, FINSEQ_1, FINSEQ_2, FUNCT_7, WSIERP_1,
      PRE_POLY, BSPACE, STRUCT_0, ALGSTR_0, RLVECT_1, GROUP_1, VECTSP_1,
      VECTSP_2, POLYNOM1, BINOM, POLYNOM2, UPROOTS, IDEAL_1, HILBASIS,
      POLYNOM7, GROEB_1, RING_1, RING_2, HILB10_5, POLYNOM9;
 constructors RELSET_1, RECDEF_1, FUNCT_7, WSIERP_1, BSPACE, GCD_1, BINOM,
      POLYNOM2, UPROOTS, HILBASIS, TERMORD, GROEB_1, HILB10_5, IDEAL_2,
      POLYNOM9;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, ORDINAL1, XREAL_0, NAT_1, CARD_1,
      VALUED_0, RELSET_1, STRUCT_0, VECTSP_1, PRE_POLY, POLYNOM2, AFINSQ_1,
      POLYNOM6, FINSEQ_1, FUNCOP_1, ALGSTR_1, ALGSTR_0, HILBASIS, POLYNOM7,
      POLYNOM1, RLVECT_1, IDEAL_1, RING_1, RING_2, PBOOLE, FINSET_1, NEWTON04,
      RVSUM_4;
 requirements NUMERALS, SUBSET, ARITHM, REAL;


begin :: Preliminaries

reserve A for non degenerated comRing;
reserve R for non degenerated domRing;
reserve n for non empty Ordinal;
reserve o,o1,o2 for object;
reserve X,Y for Subset of Funcs(n,[#]R);
reserve S,T for Subset of Polynom-Ring(n,R);
reserve F,G for FinSequence of the carrier of Polynom-Ring(n,R);
reserve x for Function of n,R;

definition
  let n be Ordinal, L be right_zeroed add-associative right_complementable
  well-unital distributive non trivial doubleLoopStr,
  p be Polynomial of n,L;
  redefine func {p} -> Subset of Polynom-Ring(n,L);
end;

::$INSERT Enhance eval function: evaluated function is Element of Polynom-Ring.
definition
  let n be Ordinal, L be right_zeroed add-associative right_complementable
  well-unital distributive non trivial doubleLoopStr;
  let f be Element of Polynom-Ring (n,L), x be Function of n,L;
  func E_eval(f,x) -> Element of L means
:: ALGGEO_1:def 1

  ex p be Polynomial of n,L st p = f & it = eval(p,x);
end;

::$INSERT Enhance E_eval function to evaluate Finite Sequence of Polynomials.
definition
   let n be Ordinal, L be right_zeroed add-associative right_complementable
   well-unital distributive non trivial doubleLoopStr,
   F be FinSequence of the carrier of Polynom-Ring(n,L),
   x be Function of n,L;
   func E_eval(F,x) -> FinSequence of the carrier of L means
:: ALGGEO_1:def 2
   dom it = dom F & for i be Nat st i in dom F holds it.i = E_eval(F/.i,x);
 end;

theorem :: ALGGEO_1:1
    for L being right_zeroed add-associative right_complementable
    well-unital distributive non trivial doubleLoopStr, n being Ordinal holds
    Support 0_(n,L) = {};

theorem :: ALGGEO_1:2
   for n being Ordinal, L being right_zeroed add-associative
   right_complementable Abelian well-unital distributive non trivial
   doubleLoopStr, f,g being Element of Polynom-Ring(n,L),
   x being Function of n, L holds
   E_eval(f+g,x) = E_eval(f,x) + E_eval(g,x);

theorem :: ALGGEO_1:3
    for n being Ordinal, L being right_zeroed add-associative
    right_complementable Abelian well-unital distributive
    non trivial commutative associative non empty doubleLoopStr,
    f,g being Element of Polynom-Ring(n,L),
    x being Function of n, L holds
    E_eval(f*g,x) = E_eval(f,x) * E_eval(g,x);

theorem :: ALGGEO_1:4
    for N0 be Nat
    for n be Ordinal,
    L being right_zeroed add-associative
    right_complementable Abelian well-unital distributive
    non trivial commutative associative non empty doubleLoopStr,
    F be FinSequence of the carrier of Polynom-Ring(n,L),
    x be Function of n,L
    st len F = N0+1 holds
    E_eval(F,x) = (E_eval(F|N0,x))^<*E_eval(F/.(len F),x)*>;

theorem :: ALGGEO_1:5
    for n be Ordinal, L being right_zeroed add-associative
    right_complementable Abelian well-unital distributive
    non trivial commutative associative non empty doubleLoopStr,
    F be FinSequence of the carrier of Polynom-Ring(n,L),
    x be Function of n,L
    holds E_eval(Sum F,x) = Sum E_eval(F,x);

::$INSERT  monic multivariate Polynomials with degree 1.
definition
   let n,R;
   let a be Function of n,R, i be Element of n;
   func deg1Poly(a,i) -> Polynomial of n,R equals
:: ALGGEO_1:def 3
   1_1(i,R) - (a.i)|(n,R);
 end;

theorem :: ALGGEO_1:6
    for a be Element of R,i be Element of n holds
    (1_1(i,R)).(UnitBag i) = 1.R & (a|(n,R)).(EmptyBag n) = a &
    (1_1(i,R)).(EmptyBag n) = 0.R & (a|(n,R)).(UnitBag i) = 0.R;

theorem :: ALGGEO_1:7
    for a be Element of R,i be Element of n holds
    1_1(i,R) is Polynomial of n,R & a|(n,R) is Polynomial of n,R;

theorem :: ALGGEO_1:8
    for a be non zero Element of R, b be Element of R,i be Element of n holds
    (a|(n,R))*'(1_1(i,R)) + b|(n,R) is Polynomial of n,R;

theorem :: ALGGEO_1:9
    for a be Element of R,i be Element of n holds
    Support((1_1(i,R)) + a|(n,R)) c= {UnitBag i} \/ {EmptyBag n};

theorem :: ALGGEO_1:10
    for n holds degree (EmptyBag n) = 0;

theorem :: ALGGEO_1:11
    for x be Element of n holds degree(UnitBag x) = 1;

theorem :: ALGGEO_1:12
    for a be Element of R,i be Element of n holds
    degree((1_1(i,R)) + a|(n,R)) = 1;

::$INSERT Enhance function Roots for multivariate Polynomials.
definition  ::Zero set of f
  let R,n;
  let f be Polynomial of n,R;
  func Zero_(f) -> Subset of Funcs(n,[#]R) equals
:: ALGGEO_1:def 4
  {x where x is Function of n,R : eval(f,x) = 0.R};
end;

::NdR Fulton69: p.9 (5): V(0) = A^n(k)
theorem :: ALGGEO_1:13
    Zero_(0_(n,R)) = Funcs(n,[#]R);

::NdR Fulton69: p.9 (5): V(1) = {}.
theorem :: ALGGEO_1:14
    Zero_(1_(n,R)) = {}Funcs(n,[#]R);

::1.2 Affine Space and Algebraic Sets
::$INSERT Enhance function Zero_ for set of Polynomials.
definition
  let R,n,S;
  func Zero_(S) -> Subset of Funcs(n,[#]R) equals
:: ALGGEO_1:def 5

  {x where x is Function of n,R : for p be Polynomial of n,R
  st p in S holds eval(p,x) = 0.R} if S <> {}
  otherwise {};
end;

theorem :: ALGGEO_1:15
    for p be Polynomial of n,R holds Zero_{p} = Zero_(p);

definition
  let R,n;
  let IT be Subset of Funcs(n,[#]R);
  attr IT is algebraic_set_from_ideal means
:: ALGGEO_1:def 6
  ex I be Ideal of Polynom-Ring(n,R) st IT = Zero_(I);
end;

registration
   let R,n;
   cluster algebraic_set_from_ideal for non empty Subset of Funcs(n,[#]R);
end;

::$INSERT Definition of Algebraic Set.
definition
   let n,R;
   mode Algebraic_Set of n,R is
     algebraic_set_from_ideal Subset of Funcs(n,[#]R);
end;

::NdR Fulton69: p.9 (3): If I c= J then V(J) c= V(I).
theorem :: ALGGEO_1:16
    for S,T be non empty Subset of Polynom-Ring(n,R) holds
    S c= T implies Zero_(T) c= Zero_(S);

::NdR Fulton69: p.9 (1): If I is the ideal in k[x1,...,xn] generated by S....
theorem :: ALGGEO_1:17
    for S be non empty Subset of Polynom-Ring(n,R) holds
    Zero_(S) = Zero_(S-Ideal);

theorem :: ALGGEO_1:18
    for I,J be Ideal of Polynom-Ring(n,R) holds
    Zero_(I \/ J) = Zero_(I) /\ Zero_(J);

theorem :: ALGGEO_1:19
    for S,T be Algebraic_Set of n,R holds S /\ T is Algebraic_Set of n,R;

definition
   let A;
   let F be non empty Subset of Ideals(A);
   redefine func union F -> non empty Subset of A;
end;

::NdR Fulton69: p.9(2):If {Ij} is any collection of ideals then V(\/Ij)=/\V(Ij)
theorem :: ALGGEO_1:20
    for F be non empty Subset of Ideals(Polynom-Ring(n,R)) holds
    Zero_(union F) = meet{Zero_I where I is Ideal of Polynom-Ring(n,R): I in F}
;

::NdR Fulton69:p.9 (4): V(FG) = V(F) \/ V(G)
theorem :: ALGGEO_1:21
    for f,g be Polynomial of n,R holds Zero_({f*'g}) = Zero_({f}) \/ Zero_({g})
;

theorem :: ALGGEO_1:22
    for I,J be Ideal of Polynom-Ring(n,R) holds
    Zero_(I /\ J) = Zero_(I) \/ Zero_(J);

theorem :: ALGGEO_1:23
    for I,J be Ideal of Polynom-Ring(n,R) holds
    Zero_(I*'J) = Zero_(I) \/ Zero_(J);

definition let n,R;
  func Alg_Sets(n,R) -> set equals
:: ALGGEO_1:def 7
  {S where S is Subset of Funcs(n,[#]R): S is Algebraic_Set of n,R };
end;

::NdR Fulton69: p.9 (4)-any finite union of algebraic sets is an algebraic set.
theorem :: ALGGEO_1:24
    for m be non zero Nat, F be Subset of Alg_Sets(n,R) holds
    card F = m implies union F is Algebraic_Set of n,R;

definition
   let n,R;
   let a be Function of n,R;
   func polyset(a) -> non empty Subset of Polynom-Ring(n,R) equals
:: ALGGEO_1:def 8
   {f where f is Polynomial of n,R:
    ex i be Element of n st f = deg1Poly(a,i)};
end;

::NdR Fulton69: p.9 (5): V(x_1-a_1,x_2-a_2,...x_n-a_n) = {(a_1,...a_n)}.
theorem :: ALGGEO_1:25
    for a be Function of n,R holds Zero_(polyset(a)) = {a};

theorem :: ALGGEO_1:26
    for x be Element of Funcs(n,[#]R) holds {x} is Algebraic_Set of n,R;

::NdR Fulton69: p.9 (5): any finite subset A^n(k) is an algebaic set.
theorem :: ALGGEO_1:27
    for m be non zero Nat, P be Subset of singletons(Funcs(n,[#]R)) holds
    card P = m implies union P is Algebraic_Set of n,R;

::$INSERT The Ideal of a Set of Points.
definition
   let R, n, X;
   func Ideal_X -> non empty Subset of Polynom-Ring(n,R) equals
:: ALGGEO_1:def 9
   {f where f is Polynomial of n,R : X c= Zero_(f)};
 end;

theorem :: ALGGEO_1:28
    for X holds Ideal_X is Ideal of Polynom-Ring(n,R);

registration
   let R, n, X;
   cluster Ideal_X -> add-closed for Subset of Polynom-Ring(n,R);
   cluster Ideal_X -> right-ideal for Subset of Polynom-Ring(n,R);
end;

::NdR Fulton69: p.11 (6): if X c= Y, then I(Y) c= I(X)
theorem :: ALGGEO_1:29
    X c= Y implies Ideal_Y c= Ideal_X;

::NdR Fulton69: p.11 (7): I({}) = k[x_1,...,x_n]
theorem :: ALGGEO_1:30
    X = {} iff Ideal_X = [#]Polynom-Ring(n,R);

::NdR Fulton69: p 11 (7): I(A^n(k)) = (0) (only (0) c= I(A^n(k)))
theorem :: ALGGEO_1:31
    {0.Polynom-Ring(n,R)} c= Ideal_([#]Funcs(n,[#]R));

::NdR Fulton69: p.11 (8): S c= I(V(S))
theorem :: ALGGEO_1:32
    S c= Ideal_(Zero_(S));

::NdR Fulton69: p.11 (8): X c= V(I(X))
theorem :: ALGGEO_1:33
    X c= Zero_(Ideal_X);

::NdR Fulton69: p.11 (9): V(I(V(S))) = V(S)
theorem :: ALGGEO_1:34
    Zero_(Ideal_(Zero_S)) = Zero_S;

::NdR Fulton69: p.11 (9): I(V(I(X))) = I(X)
theorem :: ALGGEO_1:35
    Ideal_(Zero_(Ideal_X)) = Ideal_X;

::NdR Fulton69: p.11 (9): V=V(I(V)) if V is an algebraic set.
theorem :: ALGGEO_1:36
    for X be Algebraic_Set of n,R holds X = Zero_(Ideal_X);

theorem :: ALGGEO_1:37
    for V,W be Algebraic_Set of n,R holds V = W iff Ideal_V = Ideal_W;

::NdR Fulton69: p.11 (6): case of strict subset.
theorem :: ALGGEO_1:38
    for X,Y be Algebraic_Set of n,R holds X c< Y implies Ideal_Y c< Ideal_X;

::NdR Fulton69: p.11 (10): I(X) is a radical ideal for any set X c= A^n(k).
theorem :: ALGGEO_1:39
    sqrt(Ideal_X) = Ideal_X;

::$INSERT Reducible Algebraic Set.
definition
  let R,n;
  let IT be Algebraic_Set of n,R;
  attr IT is reducible means
:: ALGGEO_1:def 10
  ex V1,V2 be Algebraic_Set of n,R st IT = V1 \/ V2 & V1 c< IT & V2 c< IT;
end;

notation
  let R,n;
  let V be Algebraic_Set of n,R;
  antonym V is irreducible for V is reducible;
end;

::NdR Fulton69:p.15 Prop1. An algebraic set V is irreducible iff I(V) is prime.
theorem :: ALGGEO_1:40
    for V be non empty Algebraic_Set of n,R holds
    V is irreducible iff Ideal_V is prime Ideal of Polynom-Ring(n,R);
