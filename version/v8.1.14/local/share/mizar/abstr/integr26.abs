:: Antiderivatives and Integration
::  by Noboru Endou
:: 
:: Received June 30, 2023
:: Copyright (c) 2023 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, REAL_1, SUBSET_1, SEQ_1, FDIFF_1, XXREAL_0, CARD_1,
      COMPLEX1, FUNCT_1, RELAT_1, ARYTM_3, NAT_1, REALSET1, ARYTM_1, VALUED_1,
      CARD_3, SEQ_2, ORDINAL2, SQUARE_1, VALUED_0, XXREAL_2, FUNCT_2, TARSKI,
      RCOMP_1, XXREAL_1, PARTFUN1, XBOOLE_0, FCONT_1, FUNCT_7, INTEGRA5,
      FINSEQ_1, ZFMISC_1, LIMFUNC2, INTEGRA1, ORDINAL4, FINSEQ_2, MEASURE7,
      SEQ_4, MEASURE5, FDIFF_3, FDIFF_12, SUPINF_1, MEMBERED, INTEGR26;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, ORDINAL1, XCMPLX_0, XREAL_0,
      NUMBERS, XXREAL_0, COMPLEX1, XXREAL_1, XXREAL_2, RELSET_1, FUNCT_1,
      PARTFUN1, FUNCT_2, VALUED_0, VALUED_1, RCOMP_1, FINSEQ_1, SEQ_1,
      RFUNCT_1, FINSEQ_2, SEQ_2, SEQ_4, FCONT_1, SUPINF_1, LIMFUNC2, FDIFF_1,
      RVSUM_1, MEASURE5, INTEGRA1, INTEGRA5, FDIFF_3, FDIFF_12, MEMBERED;
 constructors FCONT_1, RELSET_1, RVSUM_1, REAL_1, SQUARE_1, COMPLEX1, COMSEQ_2,
      MEASURE6, MESFUNC5, INTEGRA5, FDIFF_1, INTEGRA2, SEQ_4, FDIFF_3,
      FDIFF_12, LIMFUNC2;
 registrations XREAL_0, RELSET_1, INTEGRA1, RCOMP_1, INT_1, MEMBERED, XBOOLE_0,
      NUMBERS, VALUED_0, VALUED_1, FUNCT_2, XXREAL_2, SEQ_2, FCONT_3, FINSEQ_2,
      RELAT_1, ORDINAL1, XXREAL_0, NAT_1, FINSEQ_1, FDIFF_1, CARD_1, MEASURE5,
      SEQ_4, FCONT_1, RFUNCT_1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;


begin :: Basic theorems on continuity and derivativity

 reserve h,h1 for 0-convergent non-zero Real_Sequence,
         c,c1 for constant Real_Sequence;

registration
  cluster open_interval -> open for Subset of REAL;
end;

theorem :: INTEGR26:1
  for I be Interval st inf I in I holds inf I = lower_bound I;

theorem :: INTEGR26:2
  for I be interval Subset of REAL st sup I in I holds sup I = upper_bound I;

theorem :: INTEGR26:3
  for a,b be Real, I be Interval st a in I & b in I holds [.a,b.] c= I;

theorem :: INTEGR26:4
  for f be PartFunc of REAL,REAL, a,b be Real st
   a < b & [.a,b.[ c= dom f & f|([.a,b.[) is continuous &
   f is_differentiable_on ].a,b.[ & f`|(].a,b.[) is_right_convergent_in a holds
    f is_right_differentiable_in a & Rdiff(f,a) = lim_right(f`|(].a,b.[),a);

theorem :: INTEGR26:5
  for f be PartFunc of REAL,REAL, a,b be Real st
   a < b & ].a,b.] c= dom f & f|(].a,b.]) is continuous &
   f is_differentiable_on ].a,b.[ & f`|(].a,b.[) is_left_convergent_in b holds
    f is_left_differentiable_in b & Ldiff(f,b) = lim_left(f`|(].a,b.[),b);

theorem :: INTEGR26:6
  for a,b,x be Real, f be PartFunc of REAL,REAL, I be Interval st
   inf I <= a & b <= sup I & I c= dom f &
     f|I is continuous & x in ].a,b.[ holds
    f is_continuous_in x;

theorem :: INTEGR26:7
  for X be open Subset of REAL, f,F be PartFunc of REAL,REAL st
   X c= dom f & f|X is continuous holds
    for x be Real st x in X holds f is_continuous_in x;

theorem :: INTEGR26:8
  for a,b,x be Real, f be PartFunc of REAL,REAL st a <= x < b &
   ].a,b.[ c= dom f & f is_right_convergent_in x holds
   f|(].a,b.[) is_right_convergent_in x &
   lim_right(f|(].a,b.[),x) = lim_right(f,x);

theorem :: INTEGR26:9
  for a,b,x be Real, f be PartFunc of REAL,REAL st a < x <= b &
   ].a,b.[ c= dom f & f is_left_convergent_in x holds
   f|(].a,b.[) is_left_convergent_in x &
   lim_left(f|(].a,b.[),x) = lim_left(f,x);

theorem :: INTEGR26:10
  for a,b,x be Real, f be PartFunc of REAL,REAL st
   [.a,b.] c= dom f & f|[.a,b.] is continuous & x in [.a,b.[ holds
   f is_right_convergent_in x & lim_right(f|(].a,b.[),x) = f.x;

theorem :: INTEGR26:11
  for a,b,x be Real, f be PartFunc of REAL,REAL st
   [.a,b.] c= dom f & f|[.a,b.] is continuous & x in ].a,b.] holds
   f is_left_convergent_in x & lim_left(f|(].a,b.[),x) = f.x;

theorem :: INTEGR26:12
  for x be Real, f be PartFunc of REAL,REAL, I be non empty Interval,
   X be Subset of REAL st I c= X & x in I & x <> sup I holds
    f is_right_differentiable_in x iff f|X is_right_differentiable_in x;

theorem :: INTEGR26:13
  for x be Real, f be PartFunc of REAL,REAL, I be non empty Interval,
   X be Subset of REAL st I c= X & x in I & x <> inf I holds
    f is_left_differentiable_in x iff f|X is_left_differentiable_in x;

theorem :: INTEGR26:14
  for f be PartFunc of REAL,REAL, I be open Subset of REAL,
   X be Subset of REAL st I c= X holds
    f is_differentiable_on I iff f|X is_differentiable_on I;

theorem :: INTEGR26:15
  for f be PartFunc of REAL,REAL, I be non empty Interval,
      X be Subset of REAL st
   I c= X holds
    f is_differentiable_on_interval I iff f|X is_differentiable_on_interval I;

theorem :: INTEGR26:16
  for f be PartFunc of REAL,REAL, I be non empty Interval,
      X be Subset of REAL st
   I c= X & f is_differentiable_on_interval I holds f`\I = (f|X)`\I;

theorem :: INTEGR26:17
  for f be PartFunc of REAL,REAL, I,J be non empty Interval st
   f is_differentiable_on_interval I & J c= I & inf J < sup J holds
    (f`\I)|J = (f`\J);

begin :: Generalization of previous theorems

theorem :: INTEGR26:18  ::: generalized XREAL_1:5
  for a,b be R_eal st a < b ex c be Real st a < c & c < b;

theorem :: INTEGR26:19  ::: generalized ROLLE:7
  for p,q be R_eal for f be PartFunc of REAL,REAL
    st f is_differentiable_on ].p,q.[ &
    (for x be Real st x in ].p,q.[ holds diff(f,x) = 0) holds
    f|(].p,q.[) is constant;

theorem :: INTEGR26:20  ::: generalized ROLLE:8
  for p,q be R_eal, f1,f2 be PartFunc of REAL,REAL
   st f1 is_differentiable_on ].p,q.[ & f2 is_differentiable_on ].p,q.[ &
   (for x be Real st x in ].p,q.[ holds diff(f1,x) = diff(f2,x)) holds
   (f1-f2)|(].p,q.[) is constant &
   ex r be Real st for x be Real st x in ].p,q.[ holds f1.x = f2.x+r;

theorem :: INTEGR26:21 ::: generalized ROLLE:9
  for p,q be R_eal, f be PartFunc of REAL,REAL st
   f is_differentiable_on ].p,q.[
   & (for x be Real st x in ].p,q.[ holds 0<diff(f,x)) holds
    f|(].p,q.[) is increasing;

theorem :: INTEGR26:22 ::: generalized ROLLE:10
  for p,q be R_eal, f be PartFunc of REAL,REAL st
    f is_differentiable_on ].p,q.[
   & (for x be Real st x in ].p,q.[ holds diff(f,x)<0) holds
    f|(].p,q.[) is decreasing;

theorem :: INTEGR26:23 ::: generalized ROLLE:11
  for p,q be R_eal, f be PartFunc of REAL,REAL st
    f is_differentiable_on ].p,q.[
   & (for x be Real st x in ].p,q.[ holds 0<=diff(f,x)) holds
    f|(].p,q.[) is non-decreasing;

theorem :: INTEGR26:24 ::: generalized ROLLE:12
  for p,q be R_eal, f be PartFunc of REAL,REAL st
    f is_differentiable_on ].p,q.[
   & (for x be Real st x in ].p,q.[ holds diff(f,x)<=0) holds
    f|(].p,q.[) is non-increasing;

theorem :: INTEGR26:25
  for X be open Subset of REAL, x0 be Real, f be PartFunc of REAL,REAL st
   x0 in X & f is_differentiable_on X holds diff(f,x0) = diff(f|X,x0);

theorem :: INTEGR26:26  ::: generalized INTEGRA6:29
  for a,b be Real, f be PartFunc of REAL,REAL st a < b & [.a,b.] c= dom f &
   f|[.a,b.] is continuous
   ex F be PartFunc of REAL,REAL st ].a,b.[ c= dom F &
    (for x be Real st x in ].a,b.[ holds F.x = integral(f,a,x)) &
    F is_differentiable_on ].a,b.[ & F`|(].a,b.[) = f|(].a,b.[);

theorem :: INTEGR26:27  ::: generalized INTEGRA6:28
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a < b & [.a,b.] c= dom f & f|[.a,b.] is continuous & ].a,b.[ c= dom F &
   (for x be Real st x in ].a,b.[ holds F.x = integral(f,a,x)) holds
   F is_differentiable_on ].a,b.[ & F`|(].a,b.[) = f|(].a,b.[);

begin :: Antiderivatives and related theorems

definition
  let f,F be PartFunc of REAL,REAL, I be non empty Interval;
  pred F is_antiderivative_of f,I means
:: INTEGR26:def 1
  F is_differentiable_on_interval I & F`\I = f|I;
end;

theorem :: INTEGR26:28 :: corresponds to INTEGRA7:12
  for f,F,g,G be PartFunc of REAL,REAL, I be non empty Interval st
    F is_antiderivative_of f,I & G is_antiderivative_of g,I
    holds F+G is_antiderivative_of f+g,I & F-G is_antiderivative_of f-g,I;

theorem :: INTEGR26:29 :: corresponds to INTEGRA7:13
  for f,F be PartFunc of REAL,REAL, I be non empty Interval, r be Real st
   F is_antiderivative_of f,I holds r(#)F is_antiderivative_of r(#)f,I;

theorem :: INTEGR26:30  :: corresponds to INTEGRA7:14
  for f,g,F,G be PartFunc of REAL,REAL, I be non empty Interval st
   F is_antiderivative_of f,I & G is_antiderivative_of g,I holds
   F(#)G is_antiderivative_of f(#)G+F(#)g,I;

theorem :: INTEGR26:31  :: corresponds to INTEGRA7:15
  for f,g,F,G be PartFunc of REAL,REAL, I be non empty Interval st
   F is_antiderivative_of f,I & G is_antiderivative_of g,I &
   (for x be set st x in I holds G.x <> 0) holds
    F/G is_antiderivative_of (f(#)G-F(#)g)/(G(#)G),I;

theorem :: INTEGR26:32
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a <= b & [.a,b.] c= dom f & f|[.a,b.] is continuous & [.a,b.] c= dom F &
   (for x be Real st x in [.a,b.] holds F.x = integral(f,a,x)) holds
    for x be Real st x in ].a,b.[ holds
     F is_differentiable_in x & diff(F,x) = f.x;

theorem :: INTEGR26:33
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a <= b & [.a,b.] c= dom f & f|['a,b'] is bounded &
   f is_integrable_on ['a,b'] & [.a,b.] = dom F &
   for x be Real st x in [.a,b.] holds F.x = integral(f,a,x)
   holds F is Lipschitzian;

theorem :: INTEGR26:34
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a < b & [.a,b.] c= dom f & f|[.a,b.] is continuous & [.a,b.] c= dom F &
   (for x be Real st x in [.a,b.] holds F.x = integral(f,a,x)) holds
   F`|(].a,b.[) is_right_convergent_in a &
   F`|(].a,b.[) is_left_convergent_in b;

theorem :: INTEGR26:35
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a < b & [.a,b.] c= dom f & f|[.a,b.] is continuous & [.a,b.] c= dom F &
   (for x be Real st x in [.a,b.] holds F.x = integral(f,a,x)) holds
    F is_right_differentiable_in a & Rdiff(F,a) = lim_right(F`|(].a,b.[),a);

theorem :: INTEGR26:36
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a < b & [.a,b.] c= dom f & f|[.a,b.] is continuous & [.a,b.] c= dom F &
   (for x be Real st x in [.a,b.] holds F.x = integral(f,a,x)) holds
    F is_left_differentiable_in b & Ldiff(F,b) = lim_left(F`|(].a,b.[),b);

theorem :: INTEGR26:37
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a < b & [.a,b.] c= dom f & f|[.a,b.] is continuous & [.a,b.] c= dom F &
   (for x be Real st x in [.a,b.] holds F.x = integral(f,a,x)) holds
    F is_differentiable_on_interval ['a,b'] & F`\['a,b'] = f|['a,b'];

theorem :: INTEGR26:38
  for f be PartFunc of REAL,REAL, a,b be Real holds
   integral(f,b,a) = -integral(f,a,b);

theorem :: INTEGR26:39
  for a,b be Real, f,F be PartFunc of REAL,REAL st
   a < b & [.a,b.] c= dom f & f|[.a,b.] is continuous & [.a,b.] c= dom F &
   (for x be Real st x in [.a,b.] holds F.x = integral(f,a,x)) holds
    for x be Real st x in ].a,b.[ holds
     F is_differentiable_in x & diff(F,x) = f.x;

theorem :: INTEGR26:40
  for a,b be Real, f be PartFunc of REAL,REAL st
   a < b & [.a,b.] c= dom f & f|([.a,b.]) is continuous
    ex F be PartFunc of REAL,REAL st F is_antiderivative_of f,['a,b'] &
     (for x be Real st x in [.a,b.] holds F.x = integral(f,a,x));

theorem :: INTEGR26:41
  for c be Real, f,F,G be PartFunc of REAL,REAL, I be non empty Interval st
   I c= dom f & F is_antiderivative_of f,I &
   I c= dom G & (for x be Real st x in I holds G.x = F.x + c) holds
   G is_antiderivative_of f,I;

theorem :: INTEGR26:42
  for f,F be PartFunc of REAL,REAL, I,J be non empty Interval st
   inf I < sup I & I c= J & F is_antiderivative_of f,J holds
    F is_antiderivative_of f,I;

theorem :: INTEGR26:43
for a,b be Real, f be PartFunc of REAL,REAL, D be Division of ['a,b'] st
  a < b & f is_differentiable_on_interval ['a,b'] & f`\['a,b'] is bounded
  holds lower_sum((f`\['a,b'])||['a,b'],D) <= f.b-f.a &
   f.b-f.a <=upper_sum((f`\['a,b'])||['a,b'],D);

theorem :: INTEGR26:44
  for f be PartFunc of REAL,REAL, a,b be Real, I be non empty Interval
   st a in I & b in I & a < b & f is_differentiable_on_interval I &
    f`\I is_integrable_on ['a,b'] & f`\I is bounded holds
     integral(f`\['a,b'],a,b) = f.b - f.a & integral(f`\I,a,b) = f.b - f.a;

theorem :: INTEGR26:45
  for f be PartFunc of REAL,REAL, a be Real, I be non empty Interval st
   f is_differentiable_on_interval I & a in I holds integral(f`\I,a,a) = 0;

theorem :: INTEGR26:46
  for f,F,G be PartFunc of REAL,REAL, I be non empty Interval st
   F is_antiderivative_of f,I & G is_antiderivative_of f,I
   ex c be Real st for x be Real st x in I holds F.x = G.x + c;

:: Integration by substitution
theorem :: INTEGR26:47
  for a,b,p,q be Real, f,g be PartFunc of REAL,REAL st
   a < b & p < q & [.a,b.] c= dom f & f|['a,b'] is continuous &
   g is_differentiable_on_interval ['p,q'] &
   g`\['p,q'] is_integrable_on ['p,q'] & g`\['p,q'] is bounded &
   rng(g|['p,q']) c= ['a,b'] & g.p = a & g.q = b
   holds integral(f,a,b) = integral((f*g)(#)(g`\['p,q']),p,q);

:: Integration by parts
theorem :: INTEGR26:48
  for a,b be Real, f,g be PartFunc of REAL,REAL st
   a < b & f is_differentiable_on_interval ['a,b'] &
   g is_differentiable_on_interval ['a,b'] &
   f`\['a,b'] is_integrable_on ['a,b'] & f`\['a,b'] is bounded &
   g`\['a,b'] is_integrable_on ['a,b'] & g`\['a,b'] is bounded holds
    integral((f`\['a,b'])(#)g,a,b)
     = f.b * g.b - f.a * g.a - integral(f(#)(g`\['a,b']),a,b);
